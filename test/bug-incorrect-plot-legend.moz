
include ModelyzeHEOO
include Electrical

def LowPassFilterRC(cutOff : Real, n1 : Electrical , n3 : Electrical) = {
  def n2 : Electrical;
  def vOutRC : Real;
  Resistor(1000.0,n1,n2);
  Capacitor(1.0/cutOff/1000.0/2.0/3.1415,n2,n3);
  VoltageSensor(n2,n3,vOutRC);
  vOutRC = probe("Voltage Out RC");
}

def LowPassFilterRL(cutOff : Real, n1 : Electrical , n3 : Electrical) = {
  def n2 : Electrical;
  def vOutRL : Real;
  Resistor(1000.0,n2,n3);
  Inductor(1000.0/2.0/3.1415/cutOff,n1,n2);
  VoltageSensor(n2,n3,vOutRL);
  //BUG: A space in the probe text will be shown incorrectly in the plot legend.
  vOutRL = probe("Voltage Out RL");
}

def Circuit() = {
  def vp,vn : Electrical;
  def vIn : Real;
  def fSine = 2.0;
  
  SineVoltage(1.0,fSine,vp,vn);
  LowPassFilterRC(1.0,vp,vn);
  LowPassFilterRL(1.0,vp,vn);
  Ground(vn);
  VoltageSensor(vp,vn,vIn);
  vIn = probe("Voltage In");	
} 

def main = printsim(Circuit(), 0.01, 4.0)

