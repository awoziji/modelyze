/*
Modeling Kernel Language (MKL) library 
Copyright (C) 2010 David Broman

MKL library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

MKL library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with MKL library.  If not, see <http://www.gnu.org/licenses/>.
*/

include ExportModelica
include MechsysBasics
include Block
include Simulation

let DCMotor V:Voltage -> flange:Rotational -> Equations =
  let e1:Electrical in
  let e2:Electrical in
  let e3:Electrical in
  let e4:Electrical in
  SignalVoltage V e1 e4;
  Resistor 200. e1 e2;
  Inductor 0.1 e2 e3;
  EMF 1. e3 e4 flange;
  Ground e4

 //original
let Shaft flangeA:Rotational -> flangeB:Rotational -> Equations =
  let r1:Rotational in
  let r2:Rotational in
  Inertia 0.1 flangeA r1;
  Spring 3.0 r1 r2; 
  Damper 0.02 r1 r2;
  Inertia 0.1 r2 flangeB


 //original
let ControlSys =
  let s1:Signal in
  let s2:Signal in
  let s3:Signal in
  let s4:Signal in
  let r1:Rotational in
  let r2:Rotational in
  let r3:Rotational in
  ConstantSource 1.0 s1;
  Feedback s1 s4 s2;
  PID 3.0 0.7 0.1 10.0 s2 s3;
  DCMotor s3 r1;
  IdealGear 4.0 r1 r2;
  Shaft r2 r3;
  SpeedSensor r3 s4;
  probe "angularVelocity" = s4 



let main = 
  //printsim MechSys 0.1 1.
  printLine (exportModelica "ControlSys" ControlSys)
